#!/usr/bin/perl -w

#get random data from various sources and place it in the darabase

use strict;

use Fcntl qw(:DEFAULT :flock);

use Cwd;
use Cwd qw(abs_path);
use Sys::Syslog;
use File::Basename;
use File::Copy;

#######################################

my $dir = getcwd;
$ENV{PATH}="$dir/apps:/bin:/usr/bin:/usr/local/bin";

my $prefix = "/usr/local";

my $var_area = "/var/lib/randomhelper";

my $database = $var_area . "/database";

my $plugin_dir = "$prefix/share/randomhelper/plugins";

my $conf = "/etc/randomhelper";

#######################################

my $factor;

# log info
sub logit {
	my $msg = shift;
	openlog('random-collector', 'cons,pid', 'user');
	syslog('notice', $msg);
	closelog();
	print $msg . "\n";
}

sub error {
	my $msg = shift;
	logit($msg);
	exit 1;
}

# obtain data
sub get_data {
	my $src = shift;
	my $dest = shift;
	my $amount = shift;
	my $amount_bytes = $amount * 1024 * 1024;
	my $testfile=$dest.".test";
	my ($x,$buf);
	# checks
	my $src_path=$plugin_dir . "/" . $src . "/run";
	if ( -x $src_path ) {
		
		# can we get data from this source?
		chdir($plugin_dir . "/" . $src);
		system("\"$src_path\" --check");
		return 9 if ( $? ne 0 );
		
		# secure file
		if ( open(TOUCH, ">$dest") ) {
		  print TOUCH "";
		  close(TOUCH);
		  chmod 0600, $dest;
		}
		
		# run it with --get xx arg
		logit "Getting $amount mb from $src";
		chdir($plugin_dir . "/" . $src);
		system("\"$src_path\" --get $amount > \"$dest\" 2>/dev/null &");
		
		# watch it (allow for at minimum 1 mb/min transfer/create rate) 
		my $timeout = $amount * 6;
		my $pid;
		my $t = 5;
		while ( ( not defined($pid) or $pid eq "") and $t > 0 ) {
			$pid=(`pgrep -f "/bin/sh $src_path --get $amount"`)[0];
			chomp $pid if ( defined($pid) );
			if ( not defined($pid) or $pid eq "" ) {
				$pid = (`pgrep "$src_path"`)[0];
			}
			chomp $pid if ( defined($pid) );
			$t -= 1;
			sleep 1;
		}
		if ( defined($pid) and $pid ne "") {
			# allow for a 10 kb/s program/connection
			while ( (kill 0, $pid) > 0 and $timeout > 0 ) {
				sleep 10;
				$timeout -= 1;
			}
			# if it is still running, then stop it
			if ( (kill 0, $pid) > 0 ) {
				logit("Had to kill $src (pid ".(kill 15, $pid).")");
			}
		}
		else {
			logit("Unable to get pid for $src. Will wait ".
				($timeout / 6)." mins for good measure.");
			while ( $timeout > 0 ) {
				sleep 10;
				$timeout -= 1;
			}
		}
		
		# verify it actually did something
		if ( (stat($dest))[7] ne $amount_bytes ) {
			logit("Failed to run/finish " . $src . " " . 
				$amount_bytes . " > " . $dest);
			logit("Ouput was ".((stat($dest))[7])." != $amount");
			# did we get at least 10% of waht we wanted?
			if ( (stat($dest))[7] < (0.10 * $amount_bytes) ) {
				# not enough
				logit("And we did not get enough data");
				unlink($dest);
				return 5;
			}
			# otherwise just log it and continue
			logit("But will continue because we got enough (".(stat($dest))[7]." bytes)");
		}
	}
	else {
		# not supported
		logit "Plugin \"$src\" was not found in the plugin dir $plugin_dir";
		return 8;
	}
	
	# we will filter through rngtest to test for randomness, if available
	if ( open(TOUCH, ">$testfile") ) {
	  print TOUCH "";
	  close(TOUCH);
	}
	chmod 0600, $testfile;
	my $rngtest="none";
	foreach my $path ( split(/:/, $ENV{PATH} ) ) {
		if ( -x $path . "/rngtest" ) {
			$rngtest=$path . "/rngtest";
		}
	}
	if ( -x $rngtest ) {
		system("\"$rngtest\" -p < \"$dest\" > \"$testfile\" 2>/dev/null");
		if ( -s $testfile eq 0 ) {
			logit("Check of $dest was empty (rngtest: $rngtest)");
			unlink $dest;
			unlink $testfile;
			return 4;
		}
		unlink($dest);
		move($testfile, $dest);
	}
	
	return 0;
}


sub random_sort {
	my $database_name = shift;
	my $folder=shift;
	
	# file list
	if ( not opendir(DIR, $folder) ) {
		logit "Failed to open temp folder $folder for reading: $!";
		return 5;
	}
	my @files_short = grep( !/^\.{1,2}$/, readdir(DIR));
	closedir(DIR);
	
	# full paths
	my @files = ();
	foreach my $file (@files_short) {
		push @files, $folder."/".$file;
	}
	
	# determine total file sizes , for size of chunks when we mix files
	my $total_size=0;
	my $size;
	foreach my $file (@files) {
		$size = (stat($file))[7];
		$total_size += $size;
	}
	my %file_weight=();
	
	# open files in an array
	my $f=0;
	my @fh=();
	foreach my $file (@files) {
		if ( -f $file and open($fh[$f],$file) ) {
			# determine how large the chunks will be when we mix files
			$size = (stat($file))[7];
			$file_weight{$fh[$f]}=int($size / $total_size * 200);
			if ( $file_weight{$fh[$f]} < 10 ) {
				$file_weight{$fh[$f]}=10;
			}
			#print $size." -/- ".$total_size."\n";
			#print "Weght for $file: ".$file_weight{$fh[$f]} . "\n";
			#binmode($fh[$f]) || print "Failed to open in binmode $file: $!\n";
		}
		else {
			logit ("Could not open $file: $!");
		}
		$f++;
	}
	
	# open temp database
	my $database_tmp=$database_name."tmp";
	if ( not open (DATATMP , ">".$database_tmp) ) {
		logit("Exiting because could not clear tmp database: $!");
		exit;
	}
	chmod 0600, $database_tmp;
	close (DATATMP);
	if ( not open (DATATMP , "+<".$database_tmp) ) {
		logit("Exiting because could not open tmp database: $!");
		exit;
	}
	#binmode(DATATMP) || print "Failed to open database in binmode $database_tmp: $!\n";
	
	# vars
	my $added=1;
	my ($rand_amount, $buffer_s,);
	my $buffer_rand = "";
	
	while ($added eq 1) {
		$added=0;
		foreach my $fileh (@fh) {
			if ( not defined($fileh) ) {
				next;
			}
			# how many lines from this file? By file size (above)
			$rand_amount = $file_weight{$fileh} + int(rand $file_weight{$fileh});
			# print $rand_each . "-";
			read($fileh, $buffer_s, $rand_amount);
			if ( defined($buffer_s) and $buffer_s ne "" ) {
				$buffer_rand .= $buffer_s;
				$added=1;
				if ( length($buffer_rand) > 1000000 ) { # print buffer
					print DATATMP $buffer_rand;
					$buffer_rand = "";
				}
			}
		}
	}
	# print anything left
	if ( length($buffer_rand) > 0 ) {
		print DATATMP $buffer_rand;
		$buffer_rand = "";
	}
	# close and remove files
	$f=0;
	foreach my $file (@files) {
		if ( -f $file ) {
			close($fh[$f]);
			unlink $file;
			$f++;
		}
	}
	
	# open and lock file
	my $data_line;
	if ( -f $database_name ) {
		if ( not open( DATABASE , "+<".$database_name ) ) {
			logit("Exiting because could not open database for copy:$!");
			exit;
		}
		logit "Locked database";
		#binmode(DATABASE) || print "Failed to open database in binmode $database_name: $!\n";
		flock( DATABASE , LOCK_EX);
		chmod 0600, $database_name;
		# copy old databse to new one (we are adding new ones to top for random-get's speed)
		while( read(DATABASE, $buffer_s, 1000000) ) {
			print DATATMP $buffer_s;
		}
		# write it back
		seek(DATATMP,0,0); #rewind tmp
		seek(DATABASE,0,0); #rewind
		truncate(DATABASE,0); #clear file
		while( read(DATATMP, $buffer_s, 1000000) ) {
			print DATABASE $buffer_s;
		}
		close(DATABASE);
		close(DATATMP);
		unlink $database_tmp;
		logit "Unlocked database";
	}
	else {
		# first run
		move($database_tmp, $database_name);
		chmod 0600, $database_name;
	}
}


# plugin list
sub get_plugins {
	# plugin dir listing
	if ( not opendir(DIR,$plugin_dir) ) {
		logit "Could not open $plugin_dir: $!";
		return qw(size => 0);
	}
	my @p=grep( !/^\.{1,2}$/, readdir(DIR));
	closedir(DIR);
	my %plugs=();
	foreach my $plug (@p) {
		# can we get data from this source?
		chdir($plugin_dir . "/" . $plug);
		system("\"$plugin_dir/$plug/run\" --check");
		$plugs{$plug}=1 if ( $? eq 0 );
	} 
	
	# read config
	open ( CONF , $conf ) || error("Failed to open config");
	my @conf_file=<CONF>;
	close(CONF);
	my %conf_h;
	$conf_h{size}=300;
	foreach my $c (@conf_file) {
		if ( not $c =~ /^\s*#/ ) {
			# size
			$c =~ s/size\s*=\s*(\d+)/$conf_h{size}=$1/e;
			# plugins
			$c =~ s/(.+)\s*=\s*(\d+)/$conf_h{$1}=$2/e;
		}
	}
	
	# add found plugins not in config file, with default of 5
	foreach my $plug (keys %plugs) {
		if ( not exists $conf_h{$plug} ) {
			$conf_h{$plug}=5;
		}
	}
	# remove nonexistant plugins and check values
	foreach my $c (keys %conf_h) {
	  if ( $c ne "size" ) {
		if ( not exists $plugs{$c} ) {
			delete $conf_h{$c};
			logit "Error: Plugin entry $c is in config file, but there is no folder for it";
		}
		if ( not $conf_h{$c} gt 0 and not $conf_h{$c} le 10 ) {
			logit "Plugin $c had an incorrect value amount and was reset to 5";
			$conf_h{$c}=5;
		}
	  }
	}
	
	# determine total amounts for each plugin
	$factor=0;
	foreach my $c (keys %conf_h) {
	  if ( $c ne "size" ) {
		# get multiplication factor
		$factor += $conf_h{$c};
	  }
	}
	$factor=$conf_h{size} / $factor; # determine factor
	#print $factor . "\n";
	foreach my $c (keys %conf_h) {
	  if ( $c ne "size" ) {
		# apply multiplication factor
		$conf_h{$c} = int( $conf_h{$c} * $factor );
		#print "$c = $conf_h{$c}\n";
	  }
	}
	
	return %conf_h;
}


# check that we are running as owner of script   [ getlogin() is LAME ]
if ( not -w abs_path($0) ) {
	error "Needs to run as user who owns file";
}
# check that we are running as non-root
if ( -w "/" ) {
#	print "Cannot run as root!\n";
	print "Cannot run as root! Attempting to drop privleges\n";
	# try to drop privleges
	my $uid=(stat($var_area))[4];
	if ( $uid eq 0 ) {
		error "Random user home directory $var_area is owned by root!? Exiting...";
	}
	my $randuser = getpwuid($uid);
	logit "Trying as user $randuser ($uid)"; 
	my $passargs="";
	if ( $#ARGV eq 0 and $ARGV[0] eq "--config") { $passargs="--config"; }
	exec("su - \"$randuser\" -c \"".abs_path($0)." $passargs\"");
	exit;
}


# vars
my $var_area_tmp = $var_area . "/tmp";
my ($r,$format_buf);
my %plugins = ();


#check folder for permissions
if ( not -d $var_area ) {
	error "Database directory $var_area does not exists";
}
if ( not -w $var_area ) {
	error "Database directory $var_area is not writable";
}
my $mode = sprintf "%04o", ((stat($var_area))[2] & 07777);
if ( $mode ne "0700" ) {
	error "Database directory $var_area permissions are incorrect (".$mode.")";
}
if ( not -d $plugin_dir ) {
	error "Plugin directory $plugin_dir does not exist or is inaccessable";
}


# make dirs and test plugin dir
mkdir $var_area_tmp || error "Unable to create temp folder $var_area_tmp";
%plugins=get_plugins();
if ( %plugins eq 0 ) {
	error("THERE ARE NO PLUGINS INSTALLED: EXITING....");
}


# configure
if ( $#ARGV eq 0 and $ARGV[0] eq "--config") {
	if ( not opendir(DIR,$plugin_dir) ) {
		print "Could not open $plugin_dir: $!";
		exit;
	}
	my @config_plugins=grep( !/^\.{1,2}$/, readdir(DIR));
	closedir(DIR);
	for my $plugin (@config_plugins) {
		my $plugin_run=$plugin_dir . "/" . $plugin . "/" . "run";
		if ( -x $plugin_run ) {
			chdir($plugin_dir . "/" . $plugin);
			system($plugin_run." --config");
		}
	}
	exit;
}


# set higher priority
setpriority(0, 0, 2);


# wait x mins after machine startup (some generators need time to startup)
my $wait = 15 * 60;
if ( open( UPTIME, "/proc/uptime" ) ) {
	my $uptime = ( split (/ /, (<UPTIME>)[0]) )[0];
	close(UPTIME);
	if ( $uptime < $wait ) {
		sleep $wait - $uptime;
	}
}
else {
	# not linux
	sleep $wait;
}

logit "Starting";

# main
while (1) {
	
	# plugins
	%plugins=get_plugins();
	if ( %plugins eq 0 ) {
		logit "No plugins found";
		sleep 24 * 3600; # 1 day
		next;
	}
	
	# wait if we already have enough random data stocked up
	# we let it drop to half the size before filling up again
	if ( -f $database ) {
		while ( (stat($database))[7] > $plugins{size} * 1048576 / 2 ) {
			sleep(3600); # 1 hour
		}
	}
	
	# process plugins 
	logit("Getting random data");
	
	# two passes, to mix things up
	
	# first pass
	foreach my $plugin (keys %plugins) {
		if ( $plugin ne 'size' ) {
			get_data($plugin, $var_area_tmp."/".$plugin."-1", 
				int($plugins{$plugin} / 2) );
		}
	}
	# second pass
	foreach my $plugin (keys %plugins) {
		if ( $plugin ne 'size' ) {
			get_data($plugin, $var_area_tmp."/".$plugin."-2", 
				int($plugins{$plugin} / 2) );
		}
	}
	
	# randomize files
	logit("Putting data into database");
	random_sort($database, $var_area_tmp);
	logit("Finished getting random data");
	
	# At most get data twice a day - more means trouble
	sleep(12 * 3600); # 12 hours
	
}
